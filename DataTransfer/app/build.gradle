/*
 * Copyright (c) 2017 BlackBerry.  All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'

repositories {
    flatDir {
        //point to shared dir with AAR file in it
        dirs '../../../sdk'
    }
    google()
    jcenter()
}

// Create a variable called appPropertiesFile, and initialize it to your
// app.properties file, in the rootProject folder.
def appPropertiesFile = rootProject.file("app.properties")
def appProperties = new Properties()
appProperties.load(new FileInputStream(appPropertiesFile))

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
    }
    defaultConfig {
        applicationId "com.bbm.example.datatransfer"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        //Use the mockProvider flavor
        missingDimensionStrategy "authProvider", "_mockProvider"

        // The BBM Enterprise User Domain that is assigned to you.
        // For the purpose of this example app we add it here, but in a real world environment this string should be protected.
        resValue "string", "USER_DOMAIN", appProperties['user_domain']

        //Default to using BlackBerry KMS, to use Cloud key storage (ex Firebase Database) change this value to false
        buildConfigField("boolean", "USE_KMS", appProperties.getProperty("use_kms", 'true'))
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        common {
            keyAlias appProperties['keyAlias']
            keyPassword appProperties['keyPassword']
            storeFile file(appProperties['storeFile'])
            storePassword appProperties['storePassword']
        }
    }

    buildTypes {
        debug {
            debuggable true
            signingConfig signingConfigs.common
        }
        release {
            multiDexEnabled false
            minifyEnabled true
            signingConfig signingConfigs.common
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    // BBM SDK Dependencies
    implementation(name: 'bbm_sdk-release', ext: 'aar')
    implementation project(path: ':Support')
    // Other UI dependencies
    // Firebase Cloud Messaging dependencies
    // Include Google play services auth module to use Open ID.
    implementation 'com.google.guava:guava:25.1-android'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'com.android.support:cardview-v7:28.0.0'

    implementation 'com.google.android.gms:play-services-auth:16.0.0'
}
// By applying the plugin below this will ensure the project will not compile
// if google-services.json is missing. When setting up this example please
// ensure you follow https://developers.google.com/identity/sign-in/android/start
// and download the resulting google-services.json file.

apply plugin: 'com.google.gms.google-services'
